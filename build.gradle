plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'

	id 'com.github.node-gradle.node' version '5.0.0'
}

group = 'sandipchitale'
version = '0.0.1-SNAPSHOT'

node {
	download = true
	version = '18.17.0'
	npmVersion = ''

	distBaseUrl = 'https://nodejs.org/dist'

	setWorkDir(file("${project.projectDir}/.gradle/nodejs"))
	setNpmWorkDir(file("${project.projectDir}/.gradle/npm"))
	setNodeProjectDir(file("${project.projectDir}/src/main/angularapp"))
}

java {
	sourceCompatibility = '17'
}

bootWar {
	archiveFileName = 'springboot-angular-netstat.war'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.register('gradleNgBuild', NpmTask) {
	group 'angularapp build'
	description 'Builds the Angular app using ng build'

	dependsOn npmInstall
	args = ['run', 'gradle-build']

	inputs.dir file("${project.projectDir}/src/main/angularapp")
	outputs.dir file("${project.projectDir}/build/resources/main/static")
}

tasks.named('classes').configure {
	dependsOn 'gradleNgBuild'
}

tasks.named('test') {
	useJUnitPlatform()
}
